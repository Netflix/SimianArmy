// Establish version and status
ext.githubProjectName = 'SimianArmy'
group = "com.netflix.${project.name}"
apply plugin:'eclipse-wtp'

buildscript {
    repositories { mavenCentral() }
    apply from: file('gradle/buildscript.gradle'), to: buildscript
}

allprojects {
    repositories { mavenCentral() }
}

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
apply from: file('gradle/check.gradle')
// apply from: file('gradle/license.gradle')
apply from: file('gradle/release.gradle')

apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'eclipse'

dependencies {
    // for the VMWareClient
    compile 'com.cloudbees.thirdparty:vijava:5.0.0'
    compile 'dom4j:dom4j:1.6.1'

    compile 'javax.ws.rs:jsr311-api:1.1.1'
    compile 'com.sun.jersey:jersey-core:1.11'
    compile 'com.sun.jersey:jersey-server:1.11'
    compile 'com.sun.jersey:jersey-servlet:1.11'
    compile 'org.slf4j:slf4j-api:1.6.4'
    compile 'org.codehaus.jackson:jackson-core-asl:1.9.2'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.2'
    compile 'com.netflix.eureka:eureka-client:1.1.22'
    compile('com.amazonaws:aws-java-sdk:1.3.11') {
        exclude group:'org.codehaus.jackson'
    }
    compile 'commons-lang:commons-lang:2.6'
    compile 'joda-time:joda-time:2.1'

    testCompile 'org.testng:testng:6.3.1'
    testCompile 'org.mockito:mockito-core:1.8.5'

    runtime 'org.slf4j:slf4j-log4j12:1.6.1'

    // needed for findbug Missing Class: org.slf4j.impl.StaticLoggerBinder
    providedCompile 'org.slf4j:slf4j-log4j12:1.6.1'

    providedCompile 'javax.servlet:servlet-api:2.5'
    // needed for findbug Missing Class: javax.annotation.Nonnull
    providedCompile 'com.google.code.findbugs:jsr305:1.3.9'
}

test {
    useTestNG()
}

tasks.withType(Compile) {
    options.compilerArgs << "-Xlint" << "-Werror"
}

import nl.javadude.gradle.plugins.license.License
tasks.withType(License).each { licenseTask ->
    licenseTask.exclude '**/*.json'
    licenseTask.exclude '**/*.properties'
}
